use crate::frontend::{
    ast::*,
    types::*,
};

grammar;

match {
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  _
}

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// CompUnit -> [ CompUnit ] ( Decl | FuncDef )
pub SysY: CompUnit = {
    <items: (Item)*> => CompUnit { items }
}

pub Item: Item = {
    <d: Decl> => Item::Decl(d),
    <f: FuncDef> => Item::FuncDef(f),
}

// Decl -> ConstDecl | VarDecl
pub Decl: Decl = {
    <c: ConstDecl> => Decl::ConstDecl(c),
    <v: VarDecl> => Decl::VarDecl(v),
}

// BType -> 'int' | 'void' 
// void is actually FuncType, but merge them all to avoid conflicts
pub BType: Type = {
    "int" => Type::int(),
    "void" => Type::void(),
    "float" => Type::float(),
}

// ConstDecl -> 'const' BType ConstDef { ',' ConstDef } ';'
pub ConstDecl: ConstDecl = {
    "const" <ty: BType> <def: ConstDef> <mut defs: ("," <ConstDef>)*> ";" => {
        defs.insert(0, def);
        ConstDecl { ty, defs }
    }
}

pub ArrayVal: ArrayVal = {
    "{" "}" => ArrayVal::empty(),
    "{" <e: Exp> <mut vals: ("," <Exp>)*> "}" => {
        if vals.is_empty() {
            return ArrayVal::Val(e);
        }
        vals.insert(0, e);
        ArrayVal::from_vec(vals)
    },
    "{" <e: ArrayVal> <mut vals: ("," <ArrayVal>)*> "}" => {
        vals.insert(0, e);
        ArrayVal::Vals(vals)
    },
}

pub ArrayIdent: ArrayIdent = {
    <ident: Ident> "[" <sz: Int> "]" <mut size: ("[" <Int> "]")*> => {
        let mut new_size = vec![sz as usize];
        for s in size {
            new_size.push(s as usize);
        }
        ArrayIdent { ident, size: new_size }
    },
}

// ConstDef -> Ident '=' ConstInitVal
// XXX：这里貌似有点问题，const的定义不一定要有初始值
pub ConstDef: ConstDef = {
    <ident: Ident> "=" <init: ConstInitVal> => ConstDef::Val( ident, init ),
    <ident: ArrayIdent> "=" <init: ArrayVal> => ConstDef::Array( ident, init ),
}

// ConstInitVal -> ConstExp
pub ConstInitVal: Expr = {
    <e: ConstExp> => e,
}

// VarDecl -> BType VarDef { ',' VarDef } ';'
pub VarDecl: VarDecl = {
    <ty: BType> <def: VarDef> <mut defs: ("," <VarDef>)*> ";" => {
        defs.insert(0, def);
        VarDecl { ty, defs }
    }
}

// VarDef -> Ident
//         | Ident '=' InitVal
pub VarDef: VarDef = {
    <ident: Ident> <init: ("=" <InitVal>)?> => VarDef::Val ( ident, init ),
    <ident: ArrayIdent> <init: ("=" <ArrayVal>)?> => VarDef::Array ( ident, init ),
}

// InitVal -> Exp
pub InitVal: Expr = {
    <e: Exp> => e,
}

// FuncDef -> FuncType Ident '(' FuncFParams ')' Block
pub FuncDef: FuncDef = {
    <ret_ty: BType> <ident: Ident> "(" ")" <body: Block> => FuncDef { ret_ty, ident, params: vec![], body },
    <ret_ty: BType> <ident: Ident> "(" <params: FuncFParams> ")" <body: Block> => {
        FuncDef { ret_ty, ident, params, body }
    }
}

// FuncFParams -> FuncFParam { ',' FuncFParam }
pub FuncFParams: Vec<FuncFParam> = {
    <param: FuncFParam> <mut params: ("," <FuncFParam>)*> => {
        params.insert(0, param);
        params
    }
}

// FuncFParam -> BType Ident
pub FuncFParam: FuncFParam = {
    <ty: BType> <ident: Ident> => {
        FuncFParam { ty, ident }
    },
}

// Block -> '{' { BlockItem } '}'
pub Block: Block = {
    "{" <items: (BlockItem)*> "}" => Block { items }
}

// BlockItem -> Decl | Stmt
pub BlockItem: BlockItem = {
    <d: Decl> => BlockItem::Decl(d),
    <s: Stmt> => BlockItem::Stmt(s),
}

Int: i32 = {
  r"[1-9][0-9]*" => {
    let string = <>;
    i32::from_str_radix(string, 10).unwrap_or_else(|err|{
        println!("{}",err);
        i64::from_str_radix(string, 10).unwrap() as i32
    })
  },
  r"0[0-7]*" => {
    let string = <>;
    i32::from_str_radix(string, 8).unwrap_or_else(|err|{
        println!("{}",err);
        i64::from_str_radix(string, 8).unwrap() as i32
    })
  },
  r"0[xX][0-9a-fA-F]+" => {
    let string = <>;
    i32::from_str_radix(&string[2..], 16).unwrap_or_else(|err|{
        println!("{}",err);
        i64::from_str_radix(&string[2..], 16).unwrap() as i32
    })
  },
}

Float: f32 = {
  r"\.[0-9]+" => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
  r"[0-9]+\." => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
  r"[0-9]+\.[0-9]+" => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
  r"[0-9]+(\.[0-9]*)?([eE][+-]?[0-9]+)" => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
  "inf" => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
  "infinity" => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
  "Nan" => {
    let string = <>;
    string.parse::<f32>().unwrap_or_else(|err| {
        println!("{}", err);
        string.parse::<f64>().unwrap() as f32
    })
  },
}

// Number -> IntConst | floatConst
pub Number: ComptimeVal = {
  <n: Int> => ComptimeVal::int(n),
  <n: Float> => ComptimeVal::float(n),
}

// LVal -> Ident
pub LVal: LVal = {
    <ident: Ident> => LVal { ident }
}

// PrimaryExp -> '(' Exp ')' | LVal | Number
pub PrimaryExp: Expr = {
    "(" <e: Exp> ")" => e,
    <v: LVal> => Expr::lval(v),
    <n: Number> => Expr::const_(n),
}

// UnaryExp -> PrimaryExp
//           | Ident '(' [ FuncRParams ] ')'
//           | UnaryOp UnaryExp
pub UnaryExp: Expr = {
    PrimaryExp => <>,
    <ident: Ident> "(" ")" => Expr::func_call(ident, vec![]),
    <ident: Ident> "(" <arg: Exp> <mut args: ("," <Exp>)*> ")" => {
        args.insert(0, arg);
        Expr::func_call(ident, args)
    },
    "+" <e: UnaryExp> => e,
    "-" <e: UnaryExp> => {
        let zero = Expr::const_(ComptimeVal::int(0));
        Expr::binary(BinaryOp::Sub, zero, e)
    },
}

// MulExp -> UnaryExp | MulExp ('*' | '/' | '%') UnaryExp
pub MulExp: Expr = {
  UnaryExp => <>,
  <lhs: MulExp> "*" <rhs: UnaryExp> => Expr::binary(BinaryOp::Mul, lhs, rhs),
  <lhs: MulExp> "/" <rhs: UnaryExp> => Expr::binary(BinaryOp::Div, lhs, rhs),
  // TODO: Support modulo operator
  <lhs: MulExp> "%" <rhs: UnaryExp> => Expr::binary(BinaryOp::Mod, lhs, rhs),
}

// AddExp -> MulExp | AddExp ('+' | '−') MulExp
pub AddExp: Expr = {
  MulExp => <>,
  <lhs: AddExp> "+" <rhs: MulExp> => Expr::binary(BinaryOp::Add, lhs, rhs),
  <lhs: AddExp> "-" <rhs: MulExp> => Expr::binary(BinaryOp::Sub, lhs, rhs),
}

// RelExp -> AddExp | RelExp ('<' | '>' | '<=' | '>=') AddExp
pub RelExp: Expr = {
  AddExp => <>,
  // TODO: Suppoert relational operators
  <lhs: RelExp> "<" <rhs: AddExp> => Expr::binary(BinaryOp::Lt, lhs, rhs),
  <lhs: RelExp> ">" <rhs: AddExp> => Expr::binary(BinaryOp::Gt, lhs, rhs),
  <lhs: RelExp> "<=" <rhs: AddExp> => Expr::binary(BinaryOp::Le, lhs, rhs),
  <lhs: RelExp> ">=" <rhs: AddExp> => Expr::binary(BinaryOp::Ge, lhs, rhs),
}

// EqExp -> RelExp | EqExp ('==' | '!=') RelExp
pub EqExp: Expr = {
  RelExp => <>,
  // TODO: Support equality operators
  <lhs: EqExp> "==" <rhs: RelExp> => Expr::binary(BinaryOp::Eq, lhs, rhs),
  <lhs: EqExp> "!=" <rhs: RelExp> => Expr::binary(BinaryOp::Ne, lhs, rhs),
}

// LAndExp -> EqExp | LAndExp '&&' EqExp
LAndExp: Expr = {
  EqExp => <>,
  // TODO: Support logical and operator
  <lhs: EqExp> "&&" <rhs: EqExp> => Expr::binary(BinaryOp::And, lhs, rhs),
}

// LOrExp -> LAndExp | LOrExp '||' LAndExp
LOrExp: Expr = {
  LAndExp => <>,
  // TODO: Support logical or operator
  <lhs: LOrExp> "||" <rhs: LAndExp> => Expr::binary(BinaryOp::Or, lhs, rhs),
}

// Exp -> AddExp
pub Exp: Expr = AddExp => <>;

// ConstExp -> AddExp
pub ConstExp: Expr = AddExp => <>;

// Cond -> LOrExp
pub Cond: Expr = LOrExp => <>;

// Stmt -> LVal '=' Exp ';' 
//       | [ Exp ] ';' 
//       | Block
//       | 'if' '( Cond ')' Stmt [ 'else' Stmt ]
//       | 'while' '(' Cond ')' Stmt
//       | 'break' ';' 
//       | 'continue' ';'
//       | 'return' [ Exp ] ';'
// reference: https://github.com/pku-minic/kira-rs/blob/master/src/sysy.lalrpop, line 97 - 129
pub Stmt: Stmt = {
  MatchedStmt => <>,
  OpenStmt => <>,
}

pub MatchedStmt: Stmt = {
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(lval, exp),
  <expr: (Exp)?> ";" => Stmt::Expr(ExprStmt { expr }),
  <b: Block> => Stmt::Block(b),
  "if" "(" <c: Cond> ")" <s1: MatchedStmt> "else" <s2: MatchedStmt> => {
    Stmt::If(c, Box::new(s1), Some(Box::new(s2)))
  },
  "while" "(" <c: Cond> ")" <s: MatchedStmt> => Stmt::While(c, Box::new(s)),
  "break" ";" => Stmt::Break,
  "continue" ";" => Stmt::Continue,
  "return" <expr: (Exp)?> ";" => Stmt::Return(ReturnStmt { expr }),
}

pub OpenStmt: Stmt = {
  "if" "(" <c: Cond> ")" <s1: Stmt> => {
    Stmt::If(c, Box::new(s1), None)
  },
  "if" "(" <c: Cond> ")" <s1: MatchedStmt> "else" <s2: OpenStmt> => {
    Stmt::If(c, Box::new(s1), Some(Box::new(s2)))
  },
  "while" "(" <c: Cond> ")" <s: OpenStmt> => Stmt::While(c, Box::new(s)),
}
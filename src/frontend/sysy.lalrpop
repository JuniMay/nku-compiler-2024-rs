use crate::frontend::{
    ast::*,
    types::*,
};
use lexical::parse_with_options;
use lexical::options::ParseFloatOptions;

grammar;

match {
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  _
}

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// CompUnit -> [ CompUnit ] ( Decl | FuncDef )
pub SysY: CompUnit = {
    <items: (Item)*> => CompUnit { items }
}

pub Item: Item = {
    <d: Decl> => Item::Decl(d),
    <f: FuncDef> => Item::FuncDef(f),
}

// Decl -> ConstDecl | VarDecl
pub Decl: Decl = {
    <c: ConstDecl> => Decl::ConstDecl(c),
    <v: VarDecl> => Decl::VarDecl(v),
}

// BType -> 'int' | 'void' 
// void is actually FuncType, but merge them all to avoid conflicts
pub BType: Type = {
    "int" => Type::int(),
    "void" => Type::void(),
    "float" => Type::float(),
}

// ConstDecl -> 'const' BType ConstDef { ',' ConstDef } ';'
pub ConstDecl: ConstDecl = {
    "const" <ty: BType> <def: ConstDef> <mut defs: ("," <ConstDef>)*> ";" => {
        let mut defs: Vec<ConstDef> = defs.into_iter().map(|def| { def.unwrap() }).collect();
        defs.insert(0, def.unwrap());
        ConstDecl { ty, defs }
    }
}

pub ArrayVal: ArrayVal = {
    <e: Exp> => ArrayVal::Val(e),
    "{" "}" => ArrayVal::empty(),
    "{" <e: ArrayVal> <mut vals: ("," <ArrayVal>)*> "}" => {
        vals.insert(0, e);
        ArrayVal::Vals(vals)
    },
}

pub ArrayIdent: ArrayIdent = {
    <ident: Ident> "[" <sz: Exp> "]" <mut size: ("[" <Exp> "]")*> => {
        let mut new_size = vec![sz];
        for s in size {
            new_size.push(s);
        }
        ArrayIdent { ident, size: new_size }
    },
}

// ConstDef -> Ident '=' ConstInitVal
// XXX：这里貌似有点问题，const的定义不一定要有初始值
pub ConstDef: Result<ConstDef, &'static str> = {
    <ident: Ident> "=" <init: ConstInitVal> => Ok(ConstDef::Val( ident, init )),
    <ident: ArrayIdent> "=" <init: ArrayVal> => {
      if let ArrayVal::Val(_) = init {
        return Err("Array must be initialized with array value");
      }
      Ok(ConstDef::Array( ident, init ))
    },
}

// ConstInitVal -> ConstExp
pub ConstInitVal: Expr = {
    <e: ConstExp> => e,
}

// VarDecl -> BType VarDef { ',' VarDef } ';'
pub VarDecl: VarDecl = {
    <ty: BType> <def: VarDef> <mut defs: ("," <VarDef>)*> ";" => {
      let mut defs: Vec<VarDef> = defs.into_iter().map(|def| { def.unwrap() }).collect();
      defs.insert(0, def.unwrap());
      VarDecl { ty, defs }
    }
}

// VarDef -> Ident
//         | Ident '=' InitVal
pub VarDef: Result<VarDef, &'static str> = {
    <ident: Ident> <init: ("=" <InitVal>)?> => Ok(VarDef::Val ( ident, init )),
    <ident: ArrayIdent> <init: ("=" <ArrayVal>)?> => {
      if let Some(ref init) = init {
        if let ArrayVal::Val(_) = init {
          return Err("Array must be initialized with array value");
        }
      }

      Ok(VarDef::Array ( ident, init ))
    },
}

// InitVal -> Exp
pub InitVal: Expr = {
    <e: Exp> => e,
}

// FuncDef -> FuncType Ident '(' FuncFParams ')' Block
pub FuncDef: FuncDef = {
    <ret_ty: BType> <ident: Ident> "(" ")" <body: Block> => FuncDef { ret_ty, ident, params: vec![], body },
    <ret_ty: BType> <ident: Ident> "(" <params: FuncFParams> ")" <body: Block> => {
        FuncDef { ret_ty, ident, params, body }
    }
}

// FuncFParams -> FuncFParam { ',' FuncFParam }
pub FuncFParams: Vec<FuncFParam> = {
    <param: FuncFParam> <mut params: ("," <FuncFParam>)*> => {
        params.insert(0, param);
        params
    }
}

// FuncFParam -> BType Ident
pub FuncFParam: FuncFParam = {
    <ty: BType> <ident: Ident> => {
        FuncFParam { ty, ident }
    },
    <ty: BType> <ident: Ident> <indices: ("[" Exp? "]")+> => {
        let mut ty = ty;
        for (_, index, _) in indices.into_iter().skip(1) {
          ty = Type::ptr(ty);
          // ty = Type::array(ty, 0);
          // if let Some(index) = index {
          //   ty = Type::array(ty, index as usize);
          // } else {
          //   ty = Type::array(ty, 0);
          // }
        }
        let ty = Type::ptr(ty);
        FuncFParam { ty, ident }
    },
}

// Block -> '{' { BlockItem } '}'
pub Block: Block = {
    "{" <items: (BlockItem)*> "}" => Block { items }
}

// BlockItem -> Decl | Stmt
pub BlockItem: BlockItem = {
    <d: Decl> => BlockItem::Decl(d),
    <s: Stmt> => BlockItem::Stmt(s),
}

Int: i32 = {
  r"[1-9][0-9]*" => {
    let string = <>;
    i32::from_str_radix(string, 10).unwrap_or_else(|err|{
        println!("{}",err);
        i64::from_str_radix(string, 10).unwrap() as i32
    })
  },
  r"0[0-7]*" => {
    let string = <>;
    i32::from_str_radix(string, 8).unwrap_or_else(|err|{
        println!("{}",err);
        i64::from_str_radix(string, 8).unwrap() as i32
    })
  },
  r"0[xX][0-9a-fA-F]+" => {
    let string = <>;
    i32::from_str_radix(&string[2..], 16).unwrap_or_else(|err|{
        println!("{}",err);
        i64::from_str_radix(&string[2..], 16).unwrap() as i32
    })
  },
}

//iakkefloattest,original is above
Float: f64 = {
  r"\.[0-9]+" => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
  r"[0-9]+\." => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
  r"[0-9]+\.[0-9]+" => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
  r"[0-9]+(\.[0-9]*)?([eE][+-]?[0-9]+)" => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
  r"0[xX][0-9a-fA-F]+(\.[0-9a-fA-F]+)?p[+-]?[0-9]+" => {
    let string = <>;
    // TODO: 浮点数的16进制表示解析
    // let options = ParseFloatOptions::builder().build().unwrap();
    // parse_with_options::<f64, _>(&string, &options).unwrap_or_else(|err| {
    //   println!("{}", err);
    //   panic!("Failed to parse f64: {}", err);
    // })
    0.0
  },
  "inf" => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
  "infinity" => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
  "Nan" => {
    let string = <>;
    string.parse::<f64>().unwrap_or_else(|err| {
        println!("{}", err);
        panic!("Failed to parse f64: {}", err);
    })
  },
}


// Number -> IntConst | floatConst
pub Number: ComptimeVal = {
  <n: Int> => ComptimeVal::int(n),
  <n: Float> => ComptimeVal::float(n),
}

// LVal -> Ident
pub LVal: LVal = {
    <ident: Ident> => LVal { ident }
}

// PrimaryExp -> '(' Exp ')' | LVal | Number
pub PrimaryExp: Expr = {
    "(" <e: Exp> ")" => e,
    <v: LVal> => Expr::lval(v),
    <arr: ArrayIdent> => Expr::array_access(Expr::lval(LVal{ ident: arr.ident }), arr.size),
    <n: Number> => Expr::const_(n),
}

// UnaryExp -> PrimaryExp
//           | Ident '(' [ FuncRParams ] ')'
//           | UnaryOp UnaryExp
pub UnaryExp: Expr = {
    PrimaryExp => <>,
    <ident: Ident> "(" ")" => Expr::func_call(ident, vec![]),
    <ident: Ident> "(" <arg: Exp> <mut args: ("," <Exp>)*> ")" => {
        args.insert(0, arg);
        Expr::func_call(ident, args)
    },
    "+" <e: UnaryExp> => e,
    "-" <e: UnaryExp> => {
        let zero = Expr::const_(ComptimeVal::int(0));
        Expr::binary(BinaryOp::Sub, zero, e)
    },
    "!" <e: UnaryExp> => Expr::unary(UnaryOp::Not, e),
    // "~" <e: UnaryExp> => Expr::unary(UnaryOp::BitNot, e),
}

// MulExp -> UnaryExp | MulExp ('*' | '/' | '%') UnaryExp
pub MulExp: Expr = {
  UnaryExp => <>,
  <lhs: MulExp> "*" <rhs: UnaryExp> => Expr::binary(BinaryOp::Mul, lhs, rhs),
  <lhs: MulExp> "/" <rhs: UnaryExp> => Expr::binary(BinaryOp::Div, lhs, rhs),
  <lhs: MulExp> "%" <rhs: UnaryExp> => Expr::binary(BinaryOp::Mod, lhs, rhs),
}

// AddExp -> MulExp | AddExp ('+' | '−') MulExp
pub AddExp: Expr = {
  MulExp => <>,
  <lhs: AddExp> "+" <rhs: MulExp> => Expr::binary(BinaryOp::Add, lhs, rhs),
  <lhs: AddExp> "-" <rhs: MulExp> => Expr::binary(BinaryOp::Sub, lhs, rhs),
}

// RelExp -> AddExp | RelExp ('<' | '>' | '<=' | '>=') AddExp
pub RelExp: Expr = {
  AddExp => <>,
  <lhs: RelExp> "<" <rhs: AddExp> => Expr::binary(BinaryOp::Lt, lhs, rhs),
  <lhs: RelExp> ">" <rhs: AddExp> => Expr::binary(BinaryOp::Gt, lhs, rhs),
  <lhs: RelExp> "<=" <rhs: AddExp> => Expr::binary(BinaryOp::Le, lhs, rhs),
  <lhs: RelExp> ">=" <rhs: AddExp> => Expr::binary(BinaryOp::Ge, lhs, rhs),
}

// EqExp -> RelExp | EqExp ('==' | '!=') RelExp
pub EqExp: Expr = {
  RelExp => <>,
  <lhs: EqExp> "==" <rhs: RelExp> => Expr::binary(BinaryOp::Eq, lhs, rhs),
  <lhs: EqExp> "!=" <rhs: RelExp> => Expr::binary(BinaryOp::Ne, lhs, rhs),
}

// LAndExp -> EqExp | LAndExp '&&' EqExp
LAndExp: Expr = {
  EqExp => <>,
  <lhs: LAndExp> "&&" <rhs: EqExp> => Expr::binary(BinaryOp::And, lhs, rhs),
}

// LOrExp -> LAndExp | LOrExp '||' LAndExp
LOrExp: Expr = {
  LAndExp => <>,
  <lhs: LOrExp> "||" <rhs: LAndExp> => Expr::binary(BinaryOp::Or, lhs, rhs),
}

// Exp -> AddExp
pub Exp: Expr = AddExp => <>;

// ConstExp -> AddExp
pub ConstExp: Expr = AddExp => <>;

// Cond -> LOrExp
pub Cond: Expr = LOrExp => <>;

// Stmt -> LVal '=' Exp ';' 
//       | [ Exp ] ';' 
//       | Block
//       | 'if' '( Cond ')' Stmt [ 'else' Stmt ]
//       | 'while' '(' Cond ')' Stmt
//       | 'break' ';' 
//       | 'continue' ';'
//       | 'return' [ Exp ] ';'
// reference: https://github.com/pku-minic/kira-rs/blob/master/src/sysy.lalrpop, line 97 - 129
pub Stmt: Stmt = {
  MatchedStmt => <>,
  OpenStmt => <>,
}

pub MatchedStmt: Stmt = {
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(lval, exp),
  <idnet: ArrayIdent> "=" <exp: Exp> ";" => Stmt::ArrayAssign(idnet, exp),
  <expr: (Exp)?> ";" => Stmt::Expr(ExprStmt { expr }),
  <b: Block> => Stmt::Block(b),
  "if" "(" <c: Cond> ")" <s1: MatchedStmt> "else" <s2: MatchedStmt> => {
    Stmt::If(c, Box::new(s1), Some(Box::new(s2)))
  },
  "while" "(" <c: Cond> ")" <s: MatchedStmt> => Stmt::While(c, Box::new(s)),
  "break" ";" => Stmt::Break,
  "continue" ";" => Stmt::Continue,
  "return" <expr: (Exp)?> ";" => Stmt::Return(ReturnStmt { expr }),
}

pub OpenStmt: Stmt = {
  "if" "(" <c: Cond> ")" <s1: Stmt> => {
    Stmt::If(c, Box::new(s1), None)
  },
  "if" "(" <c: Cond> ")" <s1: MatchedStmt> "else" <s2: OpenStmt> => {
    Stmt::If(c, Box::new(s1), Some(Box::new(s2)))
  },
  "while" "(" <c: Cond> ")" <s: OpenStmt> => Stmt::While(c, Box::new(s)),
}